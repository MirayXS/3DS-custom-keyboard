set WORKINGPATH "0:/gm9/customkyb"
if not find $[WORKINGPATH] NULL
  echo "Patch directory not found.\nPlease follow the tutorial on GitHub."
  goto exit
end

# Cleanup
rm -o -s $[WORKINGPATH]/Dec-Keyboard-DO-NOT-INSTALL.cia
rm -o -s $[WORKINGPATH]/Patched-Keyboard-DO-NOT-INSTALL.cia

# Emunand check

set WORKINGDRIVE "1:"

if find 4:/title NULL
    if ask "Emunand detected,\ndo you want to use it?"
        set WORKINGDRIVE "4:"
    end
end


# Dumping the keyboard

# Asks the region, I'm not doing it automatically anymore
# because it didn't work for region changed systems.

labelsel -o -s "Select your region" regionmenu_*
goto exit

@regionmenu_USA
set USINGREGION "USA"
goto setvariables

@regionmenu_EUR
set USINGREGION "EUR"
goto setvariables

@regionmenu_Other
set USINGREGION "Other"
goto setvariables

@setvariables

if chk $[USINGREGION] "USA"
    set KYBPATH "$[WORKINGDRIVE]/title/00040030/0000C802/content"
    set TITLEID "000400300000C802"
    set KYBHASH "BCB4603DE9A25F5C1A1A4A5B9C78A18851456C0D9E76F9B4E430661FFBD25DDC"
elif chk $[USINGREGION] "EUR"
    set KYBPATH "$[WORKINGDRIVE]/title/00040030/0000D002/content"
    set TITLEID "000400300000D002"
    set KYBHASH "CA522F1937BD1933CA4B071C74E1FB48319E2573DCB1058396E68BCEA008B07D"
else
    echo "Your region is not supported.\nOnly USA and EUR regions are supported."
    goto exit
end


set ERRORMSG "Keyboard not found.\nMake sure you picked the right region.\nIf so, try to perform\na system update and try again."
find $[KYBPATH]/*.tmd TMD

# Build legit keyboard
set ERRORMSG "The keyboard is not legit.\nDid you install a custom one already?\nIf so, restore it first.\nFollow the uninstallation guide\nif you don't have a backup."
buildcia -l $[TMD]

set ERRORMSG ""

# Rename the og keyboard dump
find $[GM9OUT]/$[TITLEID]*.cia CIA
mv -k -n -o $[CIA] $[WORKINGPATH]/Original_Keyboard.cia

# Decrypt the keyboard to patch it
cp -w $[WORKINGPATH]/Original_Keyboard.cia $[WORKINGPATH]/Dec-Keyboard-DO-NOT-INSTALL.cia
decrypt $[WORKINGPATH]/Dec-Keyboard-DO-NOT-INSTALL.cia


set ERRORMSG "The system seems very out of date. Make sure\nthe version is over 9.0 and try again."
sha $[WORKINGPATH]/Dec-Keyboard-DO-NOT-INSTALL.cia $[KYBHASH]

set ERRORMSG "Something went wrong, do you\nhave the Patch$[USINGREGION].bps file?"
applybps $[WORKINGPATH]/Patch$[USINGREGION].bps $[WORKINGPATH]/Dec-Keyboard-DO-NOT-INSTALL.cia $[WORKINGPATH]/Patched-Keyboard-DO-NOT-INSTALL.cia
rm -o -s $[WORKINGPATH]/Dec-Keyboard-DO-NOT-INSTALL.cia



# Encrypt the patched keyboard
encrypt $[WORKINGPATH]/Patched-Keyboard-DO-NOT-INSTALL.cia
mv -k -n -o $[WORKINGPATH]/Patched-Keyboard-DO-NOT-INSTALL.cia $[WORKINGPATH]/Keyboard_patched.cia

# It seems like gm9 doesn't install system apps with a script,
# probably for security reasons or a bug,
# So I'll just print the instructions.

echo "IMPORTANT: NEVER uninstall the CFW with the\ncustom keyboard installed or you will be\nsoftbricked! If you really want to uninstall it,\nrestore the original keyboard first from here:\n(0 is the SD card)\n\n$[WORKINGPATH]/Original_Keyboard.cia"
echo "Custom Keyboard prepared successfully!\nPress A to reboot, then install it from the\nfollowing path using FBI or godmode9 and enjoy!\n\n$[WORKINGPATH]/Keyboard_patched.cia"

# cleanup and self destruct, you usually don't want this anymore
rm -o -s $[WORKINGPATH]/PatchUSA.bps
rm -o -s $[WORKINGPATH]/PatchEUR.bps
rm -o -s "0:/gm9/scripts/Custom Keyboard.gm9"

reboot

@exit